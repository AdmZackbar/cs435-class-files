<!DOCTYPE html>
<html>
<head>
    <meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
    <title>Zach Wassynger - CS 435 - Project #7</title>
    <script id="vertex-shader" type="x-shader/x-vertex">
        #define MAX_LIGHTS 3

        precision mediump float;

        attribute vec4 vPosition;
        attribute vec3 vNormal;
        attribute vec2 vTexPos;

        uniform mat4 projectionMatrix, modelMatrix, viewMatrix;
        uniform vec3 viewPosition;
        uniform struct Light
        {
            vec4 position;
            bool isActive;
            vec4 ambient;
            vec4 diffuse;
            vec4 specular;
            float attenuationCoef;
        } lights[MAX_LIGHTS];

        varying vec3 normal, surfaceToView;
        varying vec3 surfaceToLight[MAX_LIGHTS];
        varying vec2 fTexPos;
        
        void main()
        {
            vec4 worldPosition = modelMatrix * vPosition;

            normal = vNormal;
            surfaceToView = viewPosition - worldPosition.xyz;
            for (int i=0; i<MAX_LIGHTS; i++)
            {
                surfaceToLight[i] = (lights[i].position - worldPosition).xyz;
            }
            fTexPos = vTexPos;

            gl_Position = projectionMatrix * viewMatrix * modelMatrix * vPosition;
        }
    </script>
    <script id="fragment-shader" type="x-shader/x-fragment">
        #define MAX_LIGHTS 3
        
        precision mediump float;

        uniform struct Light
        {
            vec4 position;
            bool isActive;
            vec4 ambient;
            vec4 diffuse;
            vec4 specular;
            float attenuationCoef;
        } lights[MAX_LIGHTS];
        uniform struct Material
        {
            vec4 ambient;
            vec4 diffuse;
            vec4 specular;
            float shininess;
            sampler2D texture;
        } material;

        varying vec3 normal, surfaceToView;
        varying vec3 surfaceToLight[MAX_LIGHTS];
        varying vec2 fTexPos;

        vec4 applyLight(Light light, vec3 normal, vec3 surfaceToView, vec3 surfaceToLight)
        {
            float attenuation;
            vec3 surfaceToLightDir;

            if (light.position.w == 0.0)    // Directional light
            {
                surfaceToLightDir = normalize(light.position.xyz);
                attenuation = 1.0;
            } else
            {
                float distanceToLight = length(surfaceToLight);
                attenuation = 1.0 / (1.0 + light.attenuationCoef*distanceToLight*distanceToLight);

                surfaceToLightDir = normalize(surfaceToLight);
            }
            
            vec3 halfVector = normalize(surfaceToLightDir + surfaceToView);

            float Kd = dot(surfaceToLightDir, normal);
            vec4 diffuse = max(Kd, 0.0) * light.diffuse * material.diffuse;

            float Ks = pow(max(dot(normal, halfVector), 0.0), material.shininess);
            vec4 specular = Ks * light.specular * material.specular;
            
            if(Kd < 0.0) {
                specular = vec4(0.0, 0.0, 0.0, 1.0);
            }

            return (light.ambient*material.ambient) + attenuation*(diffuse + specular);
        }

        void main()
        {
            vec3 normalDir = normalize(normal);
            vec3 surfaceToViewDir = normalize(surfaceToView);

            vec4 fColor = vec4(0);
            for (int i=0; i<3; i++)
            {
                if (lights[i].isActive)
                    fColor += applyLight(lights[i], normalDir, surfaceToViewDir, surfaceToLight[i]);
            }

            vec4 texColor = texture2D(material.texture, fTexPos);

            fColor.a = 1.0;
            gl_FragColor = fColor * texColor;
        }
    </script>
    <script id="ui-vertex-shader" type="x-shader/x-vertex">
        precision mediump float;
        
        attribute vec4 vPosition;
        attribute vec2 vTexPos;

        varying vec2 fTexPos;
        
        void main()
        {
            fTexPos = vTexPos;
            gl_Position = vPosition;
        }
    </script>
    <script id="ui-fragment-shader" type="x-shader/x-vertex">
        precision mediump float;

        uniform sampler2D texture;

        varying vec2 fTexPos;
        
        void main()
        {
            gl_FragColor = texture2D(texture, fTexPos);
        }
    </script>
    <script type="text/javascript" src="../Common/webgl-utils.js"></script>
    <script type="text/javascript" src="../Common/initShaders.js"></script>
    <script type="text/javascript" src="../Common/MV.js"></script>
    <script type="text/javascript" src="proj7.js"></script>
</head>
<body>
    <canvas id="gl-canvas" width="1024" height="576">
    Canvas was not loaded - Brower does not support HTML5.
    </canvas>
    <img id="cobblestone" src="resources/textures/Cobblestone.png" hidden></img>
    <img id="wood" src="resources/textures/Wood.jpg" hidden></img>
    <img id="metal" src="resources/textures/Metal.jpg" hidden></img>
    <img id="marble" src="resources/textures/Marble.jpg" hidden></img>
    <img id="lamphead" src="resources/textures/LampHead.png" hidden></img>
    <img id="brick" src="resources/textures/Brick.jpg" hidden></img>
    <img id="sand" src="resources/textures/Sand.jpg" hidden></img>
    <img id="grass" src="resources/textures/Grass.jpg" hidden></img>
    <img id="hotbar" src="resources/ui/Hotbar.png" hidden></img>
    <img id="test" src="resources/textures/Test.png" hidden></img>
</body>
</html>
