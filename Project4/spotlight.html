<!DOCTYPE html>
<html>
<head>
<meta http-equiv="Content-Type" content="text/html;charset=utf-8" >
<title>Zach Wassynger - CS 435 - Project #4</title>
<script id="vertex-shader-v" type="x-shader/x-vertex">
attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec4 fColor;

uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform mat3 worldInvTrans;
uniform vec4 lightWorldPos;
uniform vec3 viewWorldPos;
uniform vec3 lightDirection;
uniform float shininess;
uniform float lightAngle;

void main()
{
    float attenuation = 1.0;

    vec3 surfaceWorldPos = (modelViewMatrix * vPosition).xyz;

    vec3 surfaceToLight = normalize(lightWorldPos.xyz - surfaceWorldPos);
    vec3 surfaceToView = normalize(viewWorldPos - surfaceWorldPos);
    vec3 halfVector = normalize(surfaceToLight + surfaceToView);
    vec3 normal = normalize(worldInvTrans*vNormal.xyz);

    // Compute terms in the illumination equation
    vec4 ambient = ambientProduct;

    float Kd = dot(surfaceToLight, normal);
    vec4 diffuse = max(Kd, 0.0) * diffuseProduct;

    float Ks = pow(max(dot(normal, halfVector), 0.0), shininess);
    vec4 specular = Ks * specularProduct;
    
    if(Kd < 0.0)    specular = vec4(0.0, 0.0, 0.0, 1.0);
    
    float angle = degrees(acos(dot(-surfaceToLight, normalize(lightDirection))));
    if (angle > lightAngle)   attenuation = 0.0;

    gl_Position = projectionMatrix * modelViewMatrix * vPosition;
    fColor = ambient + attenuation*(diffuse + specular);
    
    fColor.a = 1.0;
}
</script>
<script id="fragment-shader-v" type="x-shader/x-fragment">
precision mediump float;

varying vec4 fColor;

void main()
{
    gl_FragColor = fColor;
}
</script>

<script id="vertex-shader-f" type="x-shader/x-vertex">
attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec3 normal, surfaceToLight, surfaceToView;

uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightWorldPos;
uniform vec3 viewWorldPos;
uniform mat3 worldInvTrans;

void main()
{
    vec3 surfaceWorldPos = (modelViewMatrix * vPosition).xyz;

    surfaceToLight = lightWorldPos.xyz - surfaceWorldPos;
    surfaceToView = viewWorldPos - surfaceWorldPos;
    normal = worldInvTrans*vNormal.xyz;

    gl_Position = projectionMatrix * modelViewMatrix * vPosition;
}
</script>
<script id="fragment-shader-f" type="x-shader/x-fragment">
precision mediump float;

varying vec3 normal, surfaceToLight, surfaceToView;

uniform vec4 ambientProduct;
uniform vec4 diffuseProduct;
uniform vec4 specularProduct;
uniform vec3 lightDirection;
uniform float lightAngle;
uniform float shininess;

void main()
{    
    vec4 fColor;
    float attenuation = 1.0;

    vec3 norm = normalize(normal);
    vec3 surfaceToLightDir = normalize(surfaceToLight);
    vec3 surfaceToViewDir = normalize(surfaceToView);
    vec3 halfVector = normalize(surfaceToLightDir + surfaceToViewDir);

    vec4 ambient = ambientProduct;

    float Kd = dot(surfaceToLightDir, norm);
    vec4 diffuse = max(Kd, 0.0) * diffuseProduct;

    float Ks = pow(max(dot(norm, halfVector), 0.0), shininess);
    vec4 specular = Ks * specularProduct;
    
    if(Kd < 0.0) {
        specular = vec4(0.0, 0.0, 0.0, 1.0);
    }

    float angle = degrees(acos(dot(-surfaceToLightDir, normalize(lightDirection))));
    if (angle > lightAngle)   attenuation = 0.0;

    fColor = ambient + attenuation*(diffuse + specular);
    fColor.a = 1.0;

    gl_FragColor = fColor;
}
</script>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>
<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="shadedSphere2.js"></script>

</head>

<body>
<button id = "ButtonUp">Up</button>
<button id = "ButtonDown">Down</button>
<button id = "ButtonLeft">Left</button>
<button id = "ButtonRight">Right</button>
<button id = "ButtonVertex">Per-vertex Shading</button>
<button id = "ButtonFrag">Per-fragment Shading</button>
<p></p>
<div id="SliderSubdivision">
    Subdivisions 1 <input id="slide" type="range"
        min="1" max="100" step="1" value="50"
        />
        100
    </div><br/>
<p></p>
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
</body>
</html>
